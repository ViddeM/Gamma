buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:3.0.1"
    }
}

plugins {
    id "java"
    id "eclipse"
    id "application"
    id "checkstyle"
    id "pmd"
    id "nebula.lint" version "16.7.0"
}

apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
group = "it.chalmers"
version = "1.0.0-SNAPSHOT"
sourceCompatibility = 19
mainClassName = "it.chalmers.gamma.GammaApplication"

ext {
    springBootVersion = "3.0.4"
    springSecurityVersion = "6.0.2"
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor(
            // Used to generate Record Builder classes
            "io.soabase.record-builder:record-builder-processor:35",

            // Used to generate useful configuration metadata for IDEs
            "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}",
    )

    implementation(
            // Used to generate Record Builder classes
            "io.soabase.record-builder:record-builder-core:35",

            // Used to handle SQL specific errors
            "org.postgresql:postgresql:42.6.0",

            // Spring Boot
            "org.springframework.boot:spring-boot:${springBootVersion}",
            "org.springframework.boot:spring-boot-autoconfigure:${springBootVersion}",
            "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}",
            "org.springframework.boot:spring-boot-starter-security:${springBootVersion}",
            "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}",
            "org.springframework.boot:spring-boot-starter-web:${springBootVersion}",

            // OAuth2 Server
            "org.springframework.security:spring-security-oauth2-authorization-server:1.0.1",

            // Spring session
            "org.springframework.session:spring-session-core:3.0.0",

            // Jackson (JSON)
            'com.fasterxml.jackson.core:jackson-core:2.14.2',

            // Spring Data
            "org.springframework.data:spring-data-jpa:3.0.3",
            'org.springframework.data:spring-data-redis:3.0.6'
    )

    runtimeOnly(
            // FLywayDB (Database migration)
            "org.flywaydb:flyway-core:9.16.0",

            // Spring Boot
            "org.springframework.boot:spring-boot-devtools:${springBootVersion}",
            "org.springframework.boot:spring-boot-starter-data-redis:${springBootVersion}",
            "org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}",
    )

    testImplementation(
            "com.tngtech.archunit:archunit:1.0.0",
            "io.rest-assured:json-path:5.3.0",
            "io.rest-assured:rest-assured:5.3.0",
            "io.rest-assured:xml-path:5.3.0",
            "org.flywaydb:flyway-core:9.16.0",
            "org.mockito:mockito-inline:5.2.0",
            "org.springframework.boot:spring-boot-starter-test:${springBootVersion}",
            "org.springframework.security:spring-security-test:6.0.2",
            "org.testcontainers:junit-jupiter:1.17.6",
            "org.testcontainers:postgresql:1.17.6",
    )
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:1.16.3"
    }
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion = "8.11"
    ignoreFailures = false
    maxWarnings = 0
    configFile = project(":").file("config/checkstyle/checkstyle.xml")
    configProperties = ["suppressionFile": project(":").file("config/checkstyle/suppressions.xml")]
}

pmd {
    toolVersion = "6.21.0"
    consoleOutput = false
    ignoreFailures = false
    ruleSets = []
    ruleSetConfig = resources.text.fromFile("./config/pmd/ruleset.xml")
}

// Is not perfect, but gives you an idea of what needs to be changed
//gradleLint {
//    rules = ["dependency-parentheses", "archaic-wrapper"]
//}
